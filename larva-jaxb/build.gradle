apply plugin: "java"
apply plugin: "jaxb"

sourceCompatibility = 1.7
targetCompatibility = 1.7
version = "1.0"

sourceSets.main.java.srcDirs "build/generated-src/jaxb/main"
sourceSets.main.resources.srcDirs "build/schemas"

repositories {
    mavenCentral()
    maven { url "https://maven.java.net/content/groups/public/" }
}

dependencies {
    // JAXB
    jaxb "com.sun.xml.bind:jaxb-xjc:2.2.7"
    jaxb "com.sun.xml.bind:jaxb-jxc:2.2.7"

    // utilities
    compile "org.functionaljava:functionaljava:3.1"
    compile "com.google.guava:guava:15.0"
    compile "commons-lang:commons-lang:2.4"
    compile "commons-io:commons-io:2.4"

    // logging
    compile "ch.qos.logback:logback-classic:1.0.13"

    // test
    testCompile "junit:junit:4.11"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile "xmlunit:xmlunit:1.5"
}

task schemagen() << {
    file("build/schemas").mkdir()
    ant.taskdef(name: "schemagen", classname: "com.sun.tools.jxc.SchemaGenTask", classpath: sourceSets.main.compileClasspath.asPath)
    ant.schemagen(srcdir: "src/main/java", destdir: "build/schemas") {
        include(name: "larvae/jaxb/pojo/model/*.java")
        include(name: "larvae/jaxb/adapter/model/*.java")
    }
}

processResources.dependsOn schemagen

buildscript {
    repositories { mavenCentral() }
    dependencies { classpath "no.entitas.gradle.jaxb:gradle-jaxb-plugin:2.0" }
}
